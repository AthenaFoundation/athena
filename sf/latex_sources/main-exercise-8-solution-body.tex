% ------------------------------------------------------------------------
% file `main-exercise-8-solution-body.tex'
%
%     solution of type `exercise' with id `8'
%
% generated by the `solution' environment of the
%   `xsim' package v0.16a (2020/01/16)
% from source `main' on 2024/06/09 on line 763
% ------------------------------------------------------------------------
This is also trivial to prove automatically, but let's try to do it the hard way.
We start with a couple of lemmas:
\begin{tcAthena}
conclude bool-struc-lemma-1 := (forall b . b = T <==> b =/= F)
   datatype-cases bool-struc-lemma-1 {
     T => (!equiv assume (T = T) (!claim (T =/= F))
                  assume (T =/= F) (!reflex T))
   | F => (!equiv
            assume hyp := (F = T)
              (!chain-> [hyp
                    ==> (T = F)
                    ==> (T = F & T =/= F) [augment]
                    ==> false             [prop-taut]
                    ==> (F =/= F)         [(method (_ q) (!from-false q))]])
            assume hyp := (F =/= F)
              (!from-complements (F = T) (!reflex F) hyp))
   }

conclude bool-struc-lemma-2 := (forall b . b = F <==> b =/= T)
  pick-any b:Bool
    (!chain [(b = F) <==> (b =/= T) [bool-struc-lemma-1]])

conclude bool-struc-lemma :=
           (forall b1 b2 . b1 =/= b2 ==> b1 = T & b2 = F | b1 = F & b2 = T)
  pick-any b1:Bool b2:Bool
     assume hyp := (b1 =/= b2)
       (!cases (!chain<- [(b1 = T | b1 = F) <== true [(datatype-axioms "Bool")]])
         assume case1 := (b1 = T)
           (!chain-> [case1
                 ==> (T =/= b2)                           [hyp]
                 ==> (b2 =/= T)
                 ==> (b2 = F)                             [bool-struc-lemma-2]
                 ==> (b1 = T & b2 = F)                    [augment]
                 ==> (b1 = T & b2 = F | b1 = F & b2 = T)  [alternate]])
         assume case2 := (b1 = F)
           (!chain-> [case2
                 ==> (F =/= b2)                           [hyp]
                 ==> (b2 =/= F)
                 ==> (b2 = T)                             [bool-struc-lemma-1]
                 ==> (b1 = F & b2 = T)                    [augment]
                 ==> (b1 = T & b2 = F | b1 = F & b2 = T)  [alternate]]))
\end{tcAthena}
