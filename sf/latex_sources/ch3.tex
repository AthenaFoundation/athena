\chapter{Induction: Proof by Induction}

\section{Proof by Induction}

In the last chapter we proved that \smtt{Zero} is a neutral left element for addition. 
It is also a neutral element for addition on the right:
\begin{tcAthena}
define plus-n-Z := (forall n . n + 0 = n)
\end{tcAthena}
However, proving this requires induction.

In Athena, this can be proved automatically with the built-in method \smtt{induction*}:
\begin{tcAthena}
> (!induction* plus-n-Z)

Theorem: (forall ?n:Nat
           (= (plus-nat ?n:Nat Zero)
              ?n:Nat))
\end{tcAthena}
But here is a {\em declarative\/} proof of it (make sure to retract \smtt{plus-n-Z} first):
\begin{tcAthena}
by-induction plus-n-Z {  
  Zero => (!chain [(Zero + Zero) = Zero   [plus-nat-def]])
| (Succ k) => let {ih := (k + 0 = k)} 
                   # ih is our inductive hypothesis; it's already in the a.b. 
                (!chain [((Succ k) + 0) 
                       = (Succ (k + 0))   [plus-nat-def]
                       = (Succ k)         [ih]])
}
\end{tcAthena}
This proof could be shortened, e.g., we could omit the explicit justifications:
\begin{tcAthena}
by-induction plus-n-Z {  
  Zero     => (!chain [(Zero + Zero) = Zero])
| (Succ k) => (!chain [((Succ k) + 0) = (Succ (k + 0)) = (Succ k)])
}
\end{tcAthena}
Here is the corresponding proof in Idris:
\begin{idris}
plus_n_Z : (n : Nat) -> n = n + 0
plus_n_Z Z = Refl
plus_n_Z (S k) =
  let inductiveHypothesis = plus_n_Z k in
    rewrite inductiveHypothesis in Refl
\end{idris}
Note that this is a purely {\em procedural\/} proof script, not a declarative proof. It is not at all
clear how the rewriting is done---what the rewriting steps are or how they are justified. 
\begin{tcAthena}
conclude minus-diag := (forall n . n - n = Zero)
  by-induction minus-diag {
    Zero => (!chain [(Zero - Zero) = Zero          [minus-nat-def]])
  | (Succ k) => let {ih := (k - k = Zero)}
                  (!chain [((Succ k) - (Succ k))
                         = (k - k)                 [minus-nat-def]
                         = Zero                    [ih]])
  }
\end{tcAthena}
\begin{exercise}[subtitle={\mbox{\rm{\em (SF Exercise 1.0.1, p. 28)}}}]
Prove the following results: 
\begin{tcAthena}
define mult-0-r :=   (forall n . n * Zero = Zero)

define plus-n-Sm :=  (forall n m . Succ (n + m) = n + Succ m)

define plus-comm :=  (forall n m . n + m = m + n)

define plus-assoc := (forall n m k . n + (m + k) = (n + m) + k)
\end{tcAthena}
Use any previously derived results as needed. 
\end{exercise}
\begin{solution}
\begin{tcAthena}
conclude mult-0-r := (forall n . n * Zero = Zero)
  by-induction mult-0-r {
    Zero     => (!chain [(Zero * Zero) = Zero  [mult-nat-def]])
  | (Succ k) => let {ih := (k * Zero = Zero)}
                  (!chain [((Succ k) * Zero)
                         = (Zero + (k * Zero)) [mult-nat-def]
                         = (k * Zero)          [plus-nat-def]
                         = Zero                [ih]])
  }

conclude plus-n-Sm := (forall n m . Succ (n + m) = n + Succ m)
  by-induction plus-n-Sm {
    (n as Zero) => pick-any m:Nat
                     (!chain [(Succ (Zero + m)) 
                            = (Succ m)         [plus-nat-def]
                            = (Zero + Succ m)  [plus-nat-def]
                            = (n + Succ m)     [(n = Zero)]])
  | (Succ k) => 
      let {ih := (forall m . Succ (k + m) = k + Succ m)}
        pick-any m:Nat
          (!chain [(Succ ((Succ k) + m))
                 = (Succ (Succ (k + m)))       [plus-nat-def] 
                 = (Succ (k + Succ m))         [ih]
                 = ((Succ k) + (Succ m))       [plus-nat-def]])
  }

conclude plus-comm := (forall n m . n + m = m + n)
  by-induction plus-comm {
    Zero => pick-any m:Nat
              (!chain [(Zero + m)
                     = m                       [plus-nat-def]
                     = (m + Zero)              [plus-n-Z]])
  | (Succ k) => let {ih := (forall m . k + m = m + k)}
                  pick-any m:Nat               
                    (!chain [((Succ k) + m)    
                           = (Succ (k + m))    [plus-nat-def]
                           = (Succ (m + k))    [ih]
                           = (m + Succ k)      [plus-n-Sm]])
  }
 

conclude plus-assoc := (forall n m k . n + (m + k) = (n + m) + k)
  by-induction plus-assoc {
    Zero => pick-any m:Nat k:Nat
             (!chain [(Zero + (m + k)) 
                    = (m + k)                  [plus-nat-def]
                    = ((Zero + m) + k)         [plus-nat-def]])
  | (Succ n') =>                          
      let {ih := (forall m k . n' + (m + k) = (n' + m) + k)}
        pick-any m:Nat k:Nat                      
          (!chain [((Succ n') + (m + k))     
                  = (Succ (n' + (m + k)))      [plus-nat-def]
                  = (Succ ((n' + m) + k))      [ih]
                  = ((Succ (n' + m)) + k)      [plus-nat-def]
                  = (((Succ n') + m) + k)      [plus-nat-def]])
  }
\end{tcAthena}
\end{solution}
\begin{exercise}[subtitle={\mbox{\rm{\em (SF Exercise 1.0.2, p. 28)}}}]
Consider:
\begin{tcAthena}
declare double: [Nat] -> Nat [[int->Nat]]

assert* double-def := [(double Zero = Zero)
                       (double Succ n = Succ Succ double n)]
\end{tcAthena}
Prove the following: \smtt{(forall n . double n = n + n)}. 
\end{exercise}
\begin{solution}
\begin{tcAthena}
conclude double-plus := (forall n . double n = n + n)
  by-induction double-plus {
    Zero => (!chain [(double Zero)
                   = Zero                       [double-def]
                   = (Zero + Zero)              [plus-nat-def]])
  | (Succ k) => (!chain [(double Succ k) 
                       = (Succ Succ double k)   [double-def]
                       = (Succ Succ (k + k))    [(double k = k + k)] # Ind. hypothesis 
                       = (Succ ((Succ k) + k))  [plus-nat-def]
                       = (Succ (k + Succ k))    [plus-comm]
                       = ((Succ k) + (Succ k))  [plus-nat-def]])
  }
\end{tcAthena}
\end{solution}

\begin{exercise}
%\vspace*{-0.1in}
Prove the following (informally, in English): \mtt{(forall n . even n <==> \url{~}$\ls$even Succ n)}. 
\end{exercise}

\begin{solution}[print=true]
We use structural induction. When \mtt{n} is \mtt{Zero}, we need to show \mtt{(\url{~}$\ls$even Succ Zero)}, which follows
directly from the definition of \mtt{even}. 

For the inductive case, assume that \mtt{n} is of the form \mtt{(Succ k)}, so that our inductive hypothesis becomes
\begin{equation}
\mtt{(even k <==> \url{~}$\ls$even Succ k)}
\label{Eq:IndHyp}
\end{equation}
We now need to derive the following conditional:
\begin{equation}
\mtt{(even Succ k <==> \url{~}$\ls$even Succ Succ k)}. 
\label{Eq:Goal1}
\end{equation}

In the left-to-right direction, assume \mtt{(even Succ k)}. Then, from the inductive hypothesis~(\ref{Eq:IndHyp})
we infer 
\begin{equation}
\mtt{(\tneg even k)}.
\label{Eq:Aux1}
\end{equation}
But, by the definition of \mtt{even}, we have 
\begin{equation}
\mtt{(even Succ Succ k <==> even k)},
\label{Eq:EvenDef}
\end{equation}
thus~(\ref{Eq:Aux1}) becomes the desired conclusion \mtt{(\tneg even Succ Succ k)}.

For the right-to-left direction of the goal~(\ref{Eq:Goal1}), assume \mtt{(\tneg even Succ Succ k)}. 
By using~(\ref{Eq:EvenDef}) again (the definition of \mtt{even}), this assumption yields 
\mtt{(\tneg even k)}, and applying the inductive hypothesis~(\ref{Eq:IndHyp}) to 
\mtt{(\tneg even k)} yields the desired conclusion \mtt{(even Succ k)}. 
\end{solution}
\begin{exercise}[subtitle={\mbox{\rm{\em (SF Exercise 1.0.3, p. 29)}}}]
Prove the same result in Athena: \mtt{(forall n . even n <==> \url{~}$\ls$even Succ n)}. 
\end{exercise}
\begin{solution}[print=true]
\begin{tcAthena}
  by-induction evenb_S {
    Zero => (!chain [(even Zero) 
                <==> (~ even Succ Zero)                   [even-def]])
  | (Succ k) => let {ih := (even k <==> ~ even Succ k)}
                  (!chain [(even Succ k)
                      <==> (~ even k)                     [ih]
                      <==> (~ even Succ Succ k)           [even-def]])
  }
\end{tcAthena}
Note that the exact formulation of \smtt{evenb\_s} in SF (as given on p. 29) is equivalent to the above formulation
and follows from it immediately: 
\begin{tcAthena}
conclude (forall n . even Succ n <==> ~ even n) 
  pick-any n:Nat
    (!chain [(even Succ n) <==> (~ even n) [evenb_S]])
\end{tcAthena}
\end{solution}

\section{Proofs Within Proofs}
This section in the SF book appears to be about forcing assertions, which in Athena would be done via the \smkwd{force} keyword. 

In terms of composite proofs: In Coq/Idris, it seems that the main way to develop proofs is via automated rewriting and lemmas. 
This would be equivalent in style to using ATPs (automated theorem provers) in Athena and simply decomposing a top-level goal 
into a series of lemmas that could actually be handled by the ATPs. All of the ``proofs'' in such an approach would be 1-line 
proofs of the following form:
\begin{tcAthena}
define goal := ...

conclude lemma-1 := (!prove ...)

conclude lemma-n := (!prove ...) 

conclude goal := (!prove goal [lemma-1 ... lemma-n])
\end{tcAthena}
One can of course introduce additional theorem-proving tactics in Idris, though these again seem to be procedural instructions
for how to get the built-in tactics to behave properly, not declarative proofs. 
Consider:
\begin{tcAthena}
define plus-rearrange := (forall n1 n2 m1 m2 . (n1 + n2) + (m1 + m2) = (n2 + n1) + (m1 + m2))
\end{tcAthena}
This is really just a single application of commutativity of addition. 
Here is the Idris proof (as appears on p. 30):
\begin{idris}
plus_rearrange : (n, m, p, q : Nat) ->
                 (n + m) + (p + q) = (m + n) + (p + q)
plus_rearrange n m p q = rewrite plus_rearrange_lemma n m in Refl
  where
  plus_rearrange_lemma : (n, m : Nat) -> n + m = m + n
  plus_rearrange_lemma = plus_comm
\end{idris}
Here is the Athena version: 
\begin{tcAthena}
conclude plus-rearrange
  pick-any n1:Nat n2:Nat m1:Nat m2:Nat
    (!chain [((n1 + n2) + (m1 + m2)) 
           = ((n2 + n1) + (m1 + m2))   [plus-comm]])
\end{tcAthena}

\begin{exercise}[subtitle={\mbox{\rm{\em (SF Exercise 3.0.1, p. 30)}}}]
Prove: 
\begin{tcAthena}
define plus-swap := (forall n k m . n + (m + k) = m + (n + k))
define mult-comm := (forall n m . n * m = m * n)
\end{tcAthena}
\begin{solution}
\begin{tcAthena}
\end{tcAthena}
\end{solution}
